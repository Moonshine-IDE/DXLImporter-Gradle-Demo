plugins {
    id 'java'
    id 'application'  // allows 'run' task.
    id 'eclipse'      // allows dependencies to be exported to .classpath
}

// Without archivesBaseName, the default JAR will match the parent directory, which can break the agentProperties configuration
archivesBaseName='DXLImporter-Gradle-Demo'
mainClassName = 'net.prominic.demo.DemoJavaAgent'

repositories {
    mavenCentral()
}
sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
    }
}

//File importPropertiesDir = new File('agentProperties/')

String notesInstallation = project.findProperty('notesInstallation') ?: '/Applications/HCL Notes.app/Contents/MacOS/'
if (!notesInstallation) {
	throw new GradleException("Missing configured path for Notes installation.  Set notesInstallation in gradle.properties.")
}
else if (!(new File(notesInstallation).exists())) {
	throw new GradleException("Invalid configured path for Notes installation ($notesInstallation).  Check notesInstallation in gradle.properties.")
}


String notesJarBaseDir = "$notesInstallation"
String envPath = System.getenv('PATH')
if (!envPath) {
	logger.warn "Missing PATH environment variable."
	envPath = ''  // default to empty string to avoid null errors later
}


// change envPath as needed
if (notesInstallation.toLowerCase().startsWith('/applications/')) { // treat as macOS
        // directory for additional JARs is inconsistent across version, so just use `/Applications/HCL Notes.app/Contents`
        notesJarBaseDir = "$notesInstallation/.."
}
else { // treat as Windows
	// update PATH
	envPath = "$envPath;$notesInstallation"
	println "Updated PATH: $envPath"
}

/**
 * Method to find a file under the given path
 * @param baseDir  the base directory to search
 * @param findName  the name of the file to find.  TODO:  support patterns
 * @return the absolute path of the file, or null if the file was not found.
 */
String findFile(String baseDir, String findName) {
    String foundFile = null;
    // search for a file with the given pattern.  If multiple copies are found, use the last
    new File(baseDir).eachFileRecurse {File curFile ->
        if (curFile.getName() == findName) {
            foundFile = curFile.getAbsolutePath()
            println "Found file '$findName' at '${curFile.getAbsolutePath()}'"
        }
    }

    if (!foundFile) {
        println ("Could not find '$findName' in '$baseDir'")
    }

    return foundFile
}

// build a jar path to use for Notes.jar
// Supports local and Linux server executions
String notesJarPath = findFile(notesJarBaseDir, 'Notes.jar')
String jarClassPath = "./Notes.jar {{ domino_install_dir }}/jvm/lib/ext/Notes.jar ${notesJarPath.replaceAll(' ', '%20').replaceAll('^\\w+:', '/$0')}"

// Required for Java 8 Language server support
sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
	// custom environment for DXL Importer
	dxlRuntime.extendsFrom implementation  // TODO:  find a reference to the runtime classpath instead of implementation.  This is currently only needed for Notes.jar
}
dependencies {
    implementation files("$notesJarPath")

    // dependencies for importAgent
    // DXL Importer
    //dxlRuntime files('dxlLib/CheckDominoDesignElement.jar')
    dxlRuntime files('dxlLib/dxl-importer-1.1.jar')
    
    // Dependencies for DXL Importer
    dxlRuntime 'org.tmatesoft.svnkit:svnkit:1.8.5'
    dxlRuntime files(findFile(notesJarBaseDir, 'websvc.jar'))
    // New Dependencies for Domino 12.0.2
    implementation files(findFile(notesJarBaseDir, 'lwpd.domino.napi.jar'))
    implementation files(findFile(notesJarBaseDir, 'lwpd.commons.jar'))


}

/**
 * Build the JAR for an individual agent, containing only the required classes.
 */
task jarDemoJavaAgent(type: Jar, dependsOn: classes) {
	// TODO: make this a parameter for reusability?
	String agentName = 'DemoJavaAgent'
	
	// Jar name - specify the full name for reference by DemoJavaAgent.properties
	archiveFileName = "${agentName}.jar"
	
	// Optional - only include the required class files
	from sourceSets.main.output // all class files and resources
	include ("**/${agentName}.class")
}

/**
 * Import an agent using ImportDxlNsf
 * TODO:  add parameters:
 *  - agentName
 *  - type?
 * TODO:  defaults and validation for server and dbname
 * TODO: remove this if ImportAgent works better
 */
task importAgent(type: JavaExec, dependsOn:  tasks.getByName('jarDemoJavaAgent')) {
    // Include all of the required JARs for ImportDxlNsf
    classpath = configurations.dxlRuntime
    //println configurations.dxlRuntime.asPath

    main = 'com.Prominic.ImportDxlNsfForStatic'  // deprecated in later versions of Gradle - use mainClass
    //mainClass = 'com.Prominic.ImportDxlNsfForStatic'

    // need to manually set DYLD_LIBRARY_PATH, since it will not be preserved in the environment
    environment('DYLD_LIBRARY_PATH', project.findProperty('notesInstallation')?:'')

    args '-a', 'agentProperties/agentbuild/DemoJavaAgent.properties', '-t', 'agent', '-sp', 'scriptLibraryConfig/ScriptLibraryConfig.properties', '-b', '.', '-s', project.findProperty('server')?:'', '-n', project.findProperty('dbName')?:''
}

/**
 * Custom agent for running Java with Domino Dependencies
 * Set environment variables based on NotesInstallation.
 */
class RunDomino extends JavaExec {
	
	public RunDomino() {
		super()
		classpath = project.sourceSets.main.runtimeClasspath
		addDominoEnvironment()
		
		// map STDERR to STDOUT to make the output more clear in Moonshine
		errorOutput = System.out
		
		String notesIDPassword = project.findProperty('notesIDPassword')
		if (notesIDPassword) {
			// pass the password by STDIN
			// If there is a prompt for an invalid password, escape the prompt with Esc
			standardInput = new java.io.StringBufferInputStream("${notesIDPassword}\n\u001B")
		}
		else {
			// Send the Esc key to cancel any unexpected password prompts
			standardInput = new java.io.StringBufferInputStream("\u001B")
		}
	}
	
	public addDominoEnvironment() {
		String notesInstallation = project.findProperty('notesInstallation')
		String envPath = System.getenv('PATH')
		if (!envPath) {
			logger.warn "Missing PATH environment variable."
			envPath = ''  // default to empty string to avoid null errors later
		}
		
		// change envPath as needed
		if (notesInstallation.toLowerCase().startsWith('/applications/')) { // treat as macOS
			// no special logic is required
		}
		else { // treat as Windows
			// update PATH
			envPath = "$envPath;$notesInstallation"
		}
		environment('DYLD_LIBRARY_PATH', notesInstallation)
		environment('PATH', envPath)
	}
}

/** 
 * Custom Task Type for DXL Importer.
 * Allows setting some of the JavaExec properties in a reusable task
 * I based this on: https://www.baeldung.com/gradle-custom-task
 */
class ImportAgent extends RunDomino {
	@Input
	String propertiesFile
	@Input
	String type = 'agent' // "agent" or "scriptlibrary" - make this an enum?
	@Input
	String server = project.findProperty('server')
	@Input
	String dbName = project.findProperty('dbName')
        /** A value of 'debug' will show additional debugging output. */
        @Input
        String importLogMode = project.findProperty('importLogMode') ?: ''
	
	public ImportAgent() {
		super()
		classpath = project.configurations.dxlRuntime
		main = 'com.Prominic.ImportDxlNsfForStatic'
		
	}
	
	@TaskAction
	@Override
	void exec() {
		
		// validation
		if (!propertiesFile) {
			throw new GradleException("No defined agent properties file.")
		}
		if (!server) {
			throw new GradleException("No Domino server defined.  Use `-Pserver` or `gradle.properties`.")
		}
		if (!dbName) {
			throw new GradleException("No Domino dbName defined.  Use `-PdbName` or `gradle.properties`.")
		}
		
		// set args here so that it can use the configured properties
		args '-a', propertiesFile?:'undefined', 
		     '-t', this.type, 
		     '-sp', project.findProperty('scriptLibraryConfig')?:'scriptLibraryConfig/ScriptLibraryConfig.properties', 
		     '-b', '.', 
		     '-s', this.server, 
		     '-n', this.dbName


                if (this.importLogMode) { // skip for empty value
                    // add additional arguments
                    args '-l', this.importLogMode
                }
		super.exec()
	}
}

task importAllScriptLibraries() {
	// placeholder for dependencies generated by below code
}

task importAllAgents(dependsOn:  tasks.getByName('importAllScriptLibraries')) {
	// placeholder for dependencies generated by below code
	// Used to enforce that importAllScriptLibraries runs before the agents, without requring the script libraries for each agent.
}

task importAll(dependsOn: [tasks.getByName('importAllScriptLibraries'), tasks.getByName('importAllAgents')]) {
	// further dependencies should be added to importAllScriptLibraries or importAllAgents
}

// TODO: generate tasks for the script libraries

// generate tasks for agents
String agentPropertiesDirName = project.findProperty('agentPropertiesDir') ?: 'agentProperties/'
new File(agentPropertiesDirName).eachFileRecurse groovy.io.FileType.FILES, {File curFile ->
	String fullName = curFile.getName()
	int splitIndex = fullName.lastIndexOf('.')
	
	if (splitIndex >= 0) {  // confirm this is a file with the expected format
		String extension = fullName.substring(splitIndex+1)
		String agentName = fullName.substring(0, splitIndex)
		// treat any files in the "scriptbuild" directory as script libraries.  Improve this logic if needed.
		boolean isScriptLibrary = (curFile.getAbsolutePath().toLowerCase().indexOf('scriptbuild') > 0) // default all other files to be agents
		
		if (extension.equalsIgnoreCase('properties')) {
			String importTaskName = "import$agentName"
			String jarTaskName = "jar$agentName"
			// if the custom jar task doesn't exist, default to base compile
			jarTaskName = tasks.findByName(jarTaskName) ? jarTaskName : 'jar'
			
			// don't override the existing task if it exists - allow this so that the user can define a custom override
			if (!tasks.findByName(importTaskName)) {
				tasks.create(name: importTaskName, type: ImportAgent, dependsOn: tasks.getByName(jarTaskName) ) {
					propertiesFile = curFile.getAbsolutePath()
					type = isScriptLibrary ? 'scriptlibrary' : 'agent'
				}
			}
			// else:  the task already exists
			
			// add a dependency on the importAll task
			if (isScriptLibrary) {
				tasks.getByName('importAllScriptLibraries').dependsOn << importTaskName
			}
			else {  // agent
				tasks.getByName('importAllAgents').dependsOn << importTaskName
			}
			
			
		}
		else {
			println "Ignoring invalid property file:  ${curFile.getAbsolutePath()}"
		}
	}
}

